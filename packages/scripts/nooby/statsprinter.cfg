statsprinter_scriptStatus = (getRunScriptSetting "statsprinter");

if (strcmp $statsprinter_scriptStatus "true") [
	round = [
		local split;
		
		split = (strreplace $arg1 "." " ");
		result (concatword (at $split 0) "." (substr (at $split 1) 0 $arg2));
	]

	echo_stats = [
		local red blue frags deaths acc flags ldeaths kpd;
		
		red  = [ result "^f3" ]
		blue = [ result "^f1" ]
		   
		frags   = (getfrags)
		deaths  = (getdeaths)
		acc     = (getaccuracy)
		flags   = (getflags)
		   
		ldeaths = $deaths;
		if (= $ldeaths 0) [ ldeaths = 1; ]
		kpd = (round (divf $frags $ldeaths) 2)
	   
		if (> $flags 0) [ flags = (concatword (blue) " Flags: " (red) $flags); ][ flags = ""; ]
	   
		echo (concatword "^f5[ STATS ] " (blue) "Name: " (red) (getname) (blue) " Frags: " (red) $frags (blue) " Deaths: " (red) $deaths (blue) " Acc: " (red) $acc "%" (blue) " KpD: " (red) $kpd $flags);
	]
	
	bindKey "statsprinter" "echo_stats";
]
