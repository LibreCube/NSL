noobafk_scriptStatus = (getRunScriptSetting "noobafk");

// Load the script settings
	noobafk_settings = (getRunScriptOptions "noobafk");

// Init all Settings
	// Settings: Tag
	noobafk_afkTag = (getRunScriptSpecificOptions $noobafk_settings "afkTag");
	
	if (strcmp $noobafk_afkTag "") [
		noobafk_afkTag = ">AFK";
	]
		
	// Settings: TagKind - Prefix or Suffix
	noobafk_afkTagKind = (getRunScriptSpecificOptions $noobafk_settings "afkTagKind");
	
	if (strcmp $noobafk_afkTagKind "") [
		noobafk_afkTagKind = "suffix";
	]
	
	// Settings: autoAfk - Would you like to get auto spec + rename if you afk?
	noobafk_autoAfk = (getRunScriptSpecificOptions $noobafk_settings "autoAfk");
	
	if (strcmp $noobafk_autoAfk "") [
		noobafk_autoAfk = "true";
	]
	
	// Settings: secondsAutoAfk - Seconds until you get autospec + "afk" rename, minimum 30 seconds
	noobafk_secondsAutoAfk = (getRunScriptSpecificOptions $noobafk_settings "secondsAutoAfk");
	
	if (strcmp $noobafk_secondsAutoAfk "") [
		noobafk_secondsAutoAfk = 80;
	]
	
	if (< $noobafk_secondsAutoAfk 30) [
		noobafk_secondsAutoAfk = 30;
	]
		
	// Bind defaults:
	createBindIf "UP" "forward" "forward; noobafk_resetAutoAfkCounter;" 
	createBindIf "LEFT" "left" "left; noobafk_resetAutoAfkCounter;";
	createBindIf "RIGHT" "right" "right; noobafk_resetAutoAfkCounter;";
	createBindIf "DOWN" "backward" "backward; noobafk_resetAutoAfkCounter;";
		
	// Settings: All Keys
	noobafk_keyForward = (searchbinds "forward");
	noobafk_keyLeft = (searchbinds "left");
	noobafk_keyRight = (searchbinds "right");
	noobafk_keyDown = (searchbinds "backward");
	noobafk_keyAttack = (searchbinds "attack");
	noobafk_keyJump = (searchbinds "jump");
	noobafk_keyMsg = (searchbinds "saycommand");
	noobafk_keyBindManually = (getRunScriptKey "noobafk");
	
	nInit noobafk_keyForwardStore "";
	nInit noobafk_keyLeftStore "";
	nInit noobafk_keyRightStore "";
	nInit noobafk_keyDownStore "";
	nInit noobafk_keyAttackStore "";
	nInit noobafk_keyJumpStore "";
	nInit noobafk_keyMsgStore "";
	
	
	if (strcmp $noobafk_keyForward "") [
		noobafk_keyForward = $noobafk_keyForwardStore
	][
		createBindIf $noobafk_keyForwardStore  "forward; noobafk_resetAutoAfkCounter;" "forward"
		noobafk_keyForwardStore = $noobafk_keyForward;
	]
	
	if (strcmp $noobafk_keyLeft "") [
		noobafk_keyLeft = $noobafk_keyLeftStore;
	][
		createBindIf $noobafk_keyLeftStore  "left; noobafk_resetAutoAfkCounter;" "left"
		noobafk_keyLeftStore = $noobafk_keyLeft;
	]
	
	if (strcmp $noobafk_keyRight "") [
		noobafk_keyRight = $noobafk_keyRightStore;
	][
		createBindIf $noobafk_keyRightStore  "right; noobafk_resetAutoAfkCounter;" "right"
		noobafk_keyRightStore = $noobafk_keyRight;
	]
	
	if (strcmp $noobafk_keyDown "") [
		noobafk_keyDown = $noobafk_keyDownStore
	][
		createBindIf $noobafk_keyDownStore  "backward; noobafk_resetAutoAfkCounter;" "backward"
		noobafk_keyDownStore = $noobafk_keyDown;
	]
	
	if (strcmp $noobafk_keyAttack "") [
		noobafk_keyAttack = $noobafk_keyAttackStore;
	][
		createBindIf $noobafk_keyAttackStore  "attack; noobafk_resetAutoAfkCounter;" "attack"
		noobafk_keyAttackStore = $noobafk_keyAttack;
	]
	
	if (strcmp $noobafk_keyJump "") [
		noobafk_keyJump = $noobafk_keyJumpStore;
	][
		createBindIf $noobafk_keyJumpStore  "jump; noobafk_resetAutoAfkCounter;" "jump"
		noobafk_keyJumpStore = $noobafk_keyJump;
	]
	
	if (strcmp $noobafk_keyMsg "") [
		noobafk_keyMsg = $noobafk_keyMsgStore;
	][
		createBindIf $noobafk_keyMsgStore  "saycommand; noobafk_resetAutoAfkCounter;" "saycommand"
		noobafk_keyMsgStore = $noobafk_keyMsg;
	]

	noobafk_isAutoAfk = 0;
	noobafk_didIt = 0;
// End if init settings

// Is the script active?
if (= (strcmp $noobafk_scriptStatus "false") 0) [
	noobafk_do = [
		if (&& (!= $noobafk_isAutoAfk 1) (!= $noobafk_didIt 1)) [
			// Don't spec in coop mode.
			if (!= (getmode) 1) [
				spectator 1;
			]
			
			nInit noobafk_restoreName "";
			
			if (strcmp $noobafk_restoreName "") [
			
				noobafk_restoreName = (getname);
				
				if (strcmp $noobafk_afkTagKind "prefix") [
					name (concatword $noobafk_afkTag $noobafk_restoreName);
				][
					name (concatword $noobafk_restoreName $noobafk_afkTag);
				]
			]
		]
	]
	
	noobafk_undo = [
		if (|| (= $noobafk_isAutoAfk 1) (= $noobafk_didIt 1)) [
			// Not in spec in coop mode.
			if (!= (getmode) 1) [
				spectator 0;
			]
			
			name $noobafk_restoreName;
			noobafk_restoreName = "";
			
			if (strcmp $noobafk_autoAfk "true") [
				// Activate the auto afk again
				counterReset "noobafkCounter";
				noobafk_isAutoAfk = 0;
				noobafk_didIt = 0;
				noobafk_autoAfkCheck;
			]
		]
	]
	

	// Auto AFK-Function
	if (strcmp $noobafk_autoAfk "true") [
		counterStart "noobafkCounter" "1000";
		
		noobafk_autoAfkCheck = [
			sleep 5000 [
				if (= (isconnected) 1) [
					noobafk_Counter = (counterGet "noobafkCounter");
					if (>= $noobafk_Counter $noobafk_secondsAutoAfk) [
						noobafk_do;
						noobafk_isAutoAfk = 1;
					][
						if (>= $noobafk_Counter (- $noobafk_secondsAutoAfk 15)) [
							echo "^f5[ AUTO AFK ] ^f3You will put in spectator in"(- $noobafk_secondsAutoAfk $noobafk_Counter)"seconds, if you don't move, jump or shot NOW!";
						]
					]
				][
					counterReset "noobafkCounter";
				]
				
				if (= $noobafk_isAutoAfk 0) [
					noobafk_autoAfkCheck;
				]
			]
		]
		
		noobafk_autoAfkCheck;
	]	
		
		noobafk_resetAutoAfkCounter = [
			if (= $noobafk_isAutoAfk 0) [
				counterReset "noobafkCounter";
			][
				noobafk_undo;
			]
		]
		
		createBindIf $noobafk_keyForward "forward" "forward; noobafk_resetAutoAfkCounter;" 
		createBindIf $noobafk_keyLeft "left" "left; noobafk_resetAutoAfkCounter;";
		createBindIf $noobafk_keyRight "right" "right; noobafk_resetAutoAfkCounter;";
		createBindIf $noobafk_keyDown "backward" "backward; noobafk_resetAutoAfkCounter;";
		
		createBindIf $noobafk_keyAttack "attack" "attack; noobafk_resetAutoAfkCounter;";
		createBindIf $noobafk_keyJump "jump" "jump; noobafk_resetAutoAfkCounter;";
		createBindIf $noobafk_keyMsg "saycommand" "saycommand; noobafk_resetAutoAfkCounter;";
	
	// Manually AFK-Function
	noobafk_keyPress = [
		if (= (isconnected) 1) [
			//if (= (isspectator (getclientnum (getname))) 1) [
			if (= $noobafk_didIt 1) [
				noobafk_undo;
				noobafk_didIt = 0;
			][
				noobafk_do;
				noobafk_didIt = 1;
			]
		]
	]

	bindOptionalKey "noobafk" "noobafk_keyPress";
][
	// The script is deactivated. Check if the keys binds have to be reset.
	createBindIf "UP" "forward; noobafk_resetAutoAfkCounter;" "forward";
	createBindIf "LEFT" "left; noobafk_resetAutoAfkCounter;" "left" 
	createBindIf "RIGHT" "right; noobafk_resetAutoAfkCounter;" "right";
	createBindIf "DOWN" "backward; noobafk_resetAutoAfkCounter;" "backward";

	createBindIf $noobafk_keyForward "forward; noobafk_resetAutoAfkCounter;" "forward";
	createBindIf $noobafk_keyLeft "left; noobafk_resetAutoAfkCounter;" "left" 
	createBindIf $noobafk_keyRight "right; noobafk_resetAutoAfkCounter;" "right";
	createBindIf $noobafk_keyDown "backward; noobafk_resetAutoAfkCounter;" "backward";
	
	createBindIf $noobafk_keyAttack "attack; noobafk_resetAutoAfkCounter;" "attack";
	createBindIf $noobafk_keyJump "jump; noobafk_resetAutoAfkCounter;" "jump";
	createBindIf $noobafk_keyMsg "saycommand; noobafk_resetAutoAfkCounter;" "saycommand";
	
	createBindIf $noobafk_keyBindManually "noobafk_keyPress" "";
]