// Executes all scripts in a dir
execdir = [
	local file;
	
	looplist file (loop_dir_to_list $arg1 $arg2) [
		exec $file;
	]	
]

// Executes all scripts in all dirs of a list
execdirs = [
	local noob_executeDirs noobexecdirs_dir;
	noob_executeDirs = $arg1;
	
	looplist noobexecdirs_dir $noob_executeDirs [
		execdir $noobexecdirs_dir;
	]
]
	
// Loop over a dir and saves the names of the founded .cfg files into a list
loop_dir_to_list = [
	local filelist dir file subfolders;
	
	dir = $arg1;
	subfolders = $arg2; // optional
	
	filelist = [];
	
	loopfiles f $dir cfg [	
		file = (format "%1/%2.cfg" $dir $f); 
		filelist = (concat $filelist $file)
	]	
	
	if (=s $subfolders "true") [ //! find a solution how to only loop over directories
		loopfiles f $dir "" [	
			if (&& (!=s $f ".") (!=s $f "..")) [ 
				file = (format "%1/%2" $dir $f); 
				filelist = (concat $filelist (loop_dir_to_list $file));
			]
		]	
	]
	
	result $filelist;
]

// Loop over all dirs in a list, returns list with all .cfg files within the dirs
loop_dirs_to_list = [
	local noob_executeDirs noobexecdirs_dir filelist subfolders;
	
	noob_executeDirs = $arg1;
	subfolders = $arg2; // optional
	
	filelist = [];
	
	looplist noobexecdirs_dir $noob_executeDirs [
		filelist = (concat $filelist (loop_dir_to_list $noobexecdirs_dir $arg2))
	]
	
	result $filelist;
]
	
// Change a key bind, but only if a specific key have a specific bind at the moment
// @arg1 the key for the bind
// @arg2 the command which should be setted right now
// @arg3 the command which will be bind if the condition is given
createBindIf = [
	local noob_aKey noob_aKeyAction noob_newBindCommand noob_actionOfAKey;
	
	noob_aKey = $arg1;
	noob_aKeyAction = $arg2;
	noob_newBindCommand = $arg3;
	
	noob_actionOfAKey = (getbind $noob_aKey);
	
	if (=s $noob_actionOfAKey $noob_aKeyAction) [
		bind $noob_aKey $noob_newBindCommand;
	]	
]

// Count up
counterProgress = [
	local noob_CounterNameProgress;
	
	noob_CounterNameProgress = $arg1;
	
	sleep (getalias (concatword "noob_CounterInterval_" $noob_CounterNameProgress)) [
		if (= (getalias (concatword "noob_CounterStatus_" $noob_uniqueCounterName)) 1) [
		
			(concatword "noob_Counter_" $noob_CounterNameProgress) = (+ (getalias (concatword "noob_Counter_" $noob_CounterNameProgress)) 1);
		
			counterProgress $noob_CounterNameProgress;
		]
	]
]

// Starts a counter
counterStart = [
	local noob_uniqueCounterName noob_counterInterval;
	
	noob_uniqueCounterName = $arg1;
	noob_counterInterval = $arg2;
	
	(concatword "noob_Counter_" $noob_uniqueCounterName) = 0;
	(concatword "noob_CounterStatus_" $noob_uniqueCounterName) = 1;
	(concatword "noob_CounterInterval_" $noob_uniqueCounterName) = $noob_counterInterval;
	
	counterProgress $noob_uniqueCounterName;
]

// Stops a counter
counterStop = [
	local noob_uniqueCounterName;
	
	noob_uniqueCounterName = $arg1;

	(concatword "noob_CounterStatus_" $noob_uniqueCounterName) = 0;
	result (getalias (concatword "noob_Counter_" $noob_uniqueCounterName));
	(concatword "noob_Counter_" $noob_uniqueCounterName) = 0;
]

// Get the value of a counter
counterGet = [
	local noob_uniqueCounterName;
	
	noob_uniqueCounterName = $arg1;
	
	result (getalias (concatword "noob_Counter_" $noob_uniqueCounterName));
]

// Resets a counter
counterReset = [
	local noob_uniqueCounterName noob_newcounterInterval;
	
	noob_uniqueCounterName = $arg1;
	noob_newcounterInterval = $arg2; // optional
	
	(concatword "noob_Counter_" $noob_uniqueCounterName) = 0;
	if (!=s $noob_newcounterInterval "") [
		(concatword "noob_CounterInterval_" $noob_uniqueCounterName) = $noob_newcounterInterval;
	]
]

// Returns 1 (= true) if the mode is a teammode, -1 for false
isTeammode = [
	local noob_checkMode noob_teammodes;
	
	noob_checkMode = $arg1;
	
	noob_teammodes = [2 4 6 8 9 10 11 12 13 14 15 16 17 18 19 20 21 22];
	
	if (!= (indexof $noob_teammodes $noob_checkMode) -1) [
		result 1;
	][
		result -1;
	]
]

// Checks if variable exists, () == null
nIsDef = [ 
	if (=s (getalias $arg1) "") [ result 0 ] [ result 1 ] 
] 

// Set the var value, if the var dont have already a value
nInit = [ 
	if (! (nIsDef $arg1)) [alias $arg1 $arg2; result 1 ] [ result 0 ] 
]

"++" = [
	if (= (nIsDef $arg1) 1) [
		alias $arg1 (+ (getalias $arg1) 1);
	][
		alias $arg1 1;
	]
]

"--" = [
	if (= (nIsDef $arg1) 1) [
		alias $arg1 (- (getalias $arg1) 1);
	][
		alias $arg1 -1;
	]
]

nListSaveElement = [
	// usage: $arg1 = the list alias; $arg2 = a unique key (unique in this list); $arg3 = value
	// e.g. zListSaveElement zTodo "cube-corp" "motivational jpgs"
	if (=s $arg2 "") [
		result -1;
	][
		if (=s $arg3 "") [
			zListDeleteElement $arg1 $arg2; // if there is a empty value delete the element from the list 
		][
			arg3 = (escape $arg3); // make sure it can handle every kind of chars
	
			if (= (indexof (getalias $arg1) $arg2) -1) [
				alias $arg1 (concat (getalias $arg1) (concatword "^"" $arg2 "^" ^"" $arg3 "^""))
			][
				zListDeleteElement $arg1 $arg2;
				alias $arg1 (concat (getalias $arg1) (concatword "^"" $arg2 "^" ^"" $arg3 "^""))
			]
		]
	]
]

nListDeleteElement = [
	// usage: $arg1 = the list alias; $arg2 = a unique key (unique in this list)
	// e.g. zListDeleteElement zTodo "cube-corp"
	local zTMPDeleteList;
	
	zTMPDeleteList = (getalias $arg1)
	if (= (indexof $zTMPDeleteList $arg2) -1) [
		result -1;
	][
		alias $arg1 (listsplice $zTMPDeleteList () (indexof $zTMPDeleteList $arg2) 2)
	]
]

nListGetValue = [
	// usage: $arg1 = the list alias; $arg2 = a unique key (unique in this list) or list of keys for nested lists; $arg3 = mode (local, default global)
	// e.g. echo (zListGetValue zTodo "cube-corp_EN")
	local zTMPGetList value key;
	
	if (=s $arg3 "local") [
		zTMPGetList = $arg1;
	][
		zTMPGetList = (getalias $arg1);
	]
	
	looplist key $arg2 [
		if (= (indexof $zTMPGetList $key) -1) [
			zTMPGetList = -1;
		][
			zTMPGetList = (at $zTMPGetList (+ (indexof $zTMPGetList $key) 1))
		]
	]
	
	result (unescape $zTMPGetList);
]

///////////////////////////////////////////////////////////////////////////////////////////////////////
///// Nooby's Script Library | License: Creative Commons Attribution-ShareAlike 4.0 International     /
///// Website: http://noobys.org																	  /
///// Readme: packages/scripts/nsl_readme.txt														  /
///////////////////////////////////////////////////////////////////////////////////////////////////////

nslVersion = "0.9";

nslDefaultSauerFiles = [auth.cfg autoexec.cfg config.cfg init.cfg restore.cfg rpg_config.cfg rpg_restore.cfg servers.cfg server-init.cfg]

// init the nsl
nsl_init = [
	nslSettings = $arg1;
	nslScriptDirectory = $arg2;
	
	execdir (concatword $nslScriptDirectory "nsl") "true";

	//exec_outlaw ""; executes scripts from the root directory, works fine, just add an option to toggle it on/off
	
	exec_outlaw (concatword $nslScriptDirectory "outlaw");
	
	nslAllFiles = (loop_dir_to_list (concatword $nslScriptDirectory "nsl") "true");
	nslAllOutlawFiles = (loop_dir_to_list (concatword $nslScriptDirectory "outlaw"));
]

// run outlaw scripts
exec_outlaw = [
	local dir f file;
	dir = $arg1;
	
	loopfiles f $dir cfg [	
		if (!=s (nsl_is_script_enabled (concatword "outlaw/" $f ".cfg") "false") "false") [
			file = (format "%1/%2.cfg" $dir $f); 
			exec $file;
		]
 	]	
]


// Results the data string of the script
// @arg1 script name
// @return data string of the script
// @example nsl_get_script_data "nooby/autosorry"
nsl_get_script_data = [
	result (nListGetValue nslSettings $arg1)
]

getRunScript = [
	local noob_scriptName;
	noob_scriptName = $arg1;
	
	result (indexof $nslSettings $noob_scriptName);
]


// Results if a script should be execute or not
// @arg1 script name
// @arg2 default setting, used if there isn't a setting saved (optional)
// @return results true or false
// @example nsl_is_script_enabled "autosorry"
nsl_is_script_enabled = [
	local noob_scriptName scriptData;
	
	noob_scriptName = $arg1;
	scriptData = (nsl_get_script_data $noob_scriptName);
	
	if (= $scriptData -1) [
		if (!=s $arg2 "") [
			result $arg2;
		][
			result "true";
		]
	][
		result (nListGetValue $scriptData "enabled" "local")
	]
]

nsl_from_file_to_script_name =  [
	local scriptName;
	
	scriptName = $arg1;
	
	scriptName = (strreplace $scriptName $nslScriptDirectory "")
	scriptName = (strreplace $scriptName "nsl/"	"")
	
	result $scriptName;
]

nsl_toggle_script = [
	echo $arg1;
]

// Returns the key for a bind for a specific script
// @arg1 script name
// @return returns a shortcut for a key
// @example nsl_get_script_key "nooby/autosorry"
nsl_get_script_key = [
	local scriptData;
	
	scriptData = (nsl_get_script_data $arg1)
	result (nListGetValue $scriptData "key" "local")
]

getRunScriptKey = [
	// local noob_scriptName noob_indexMapData;
	// noob_scriptName = $arg1;
	
	// noob_indexMapData = (getRunScript $noob_scriptName);
	// result (at $nslSettings (+ $noob_indexMapData 2));
	nsl_get_script_key $arg1;
	echo (concatword $arg1 " verwendet immer noch getRunScriptKey!");
]

// Returns the complete options for a script
// @arg1 script name
// @return returns a cubescript list with all options
// @example nsl_get_script_options_data "nooby/autosorry"
nsl_get_script_options_data = [
	local noob_scriptName scriptOptions;
	noob_scriptName = $arg1;
	
	scriptOptions = (nListGetValue nslSettings $noob_scriptName)
	
	result (nListGetValue $scriptOptions "settings" "local")	
]

getRunScriptOptions = [
	nsl_get_script_options_data	$arg1;
	echo (concatword $arg1 " verwendet immer noch getRunScriptOptions!");
]

getRunScriptSpecificOptions = [
	nsl_get_script_option $arg1 $arg2 $arg3;
	echo (concat $arg2 "option - uses still getRunScriptSpecificOptions!");
]

// Results a specific setting out of a settings list
// @arg1 list with settings e.g. [optionName "optionValue" optionName2 "optionValue2" ...]
// @arg2 optionName
// @arg3 default value, if there isn't a setting saved (optional)
// @return Results the one options value which you are need
// @example nsl_get_script_option [sayto "team" srymsg "Sorry for that teamkill"] "srymsg"
nsl_get_script_option = [
	local noob_runScriptOptions noob_runScriptKey indexSpecificOption;

	noob_runScriptOptions = $arg1; // The options list (poor "array" xD)
	noob_runScriptKey = $arg2; // You want this one option
	
	indexSpecificOption = (nListGetValue $noob_runScriptOptions $noob_runScriptKey "local")
	
	if (= $indexSpecificOption -1) [
		if (!=s $arg3 "") [
			result $arg3;
		][
			result -1;
		]
	][
		result $indexSpecificOption;
	]
]

// Binds the setted key (in the settings) with a specific function
// @arg1 script name
// @arg2 script function which will be bind to the key
// @arg3 optional - kind of the bind - have to be bind (default), editbind or specbind
// @return 
// @example bindKey "statsprinter" "print_stats"
bindKey = [
	local noob_scriptName noob_scriptAction noob_keyKind noob_scriptKey;
	
	noob_scriptName = $arg1;
	noob_scriptAction = $arg2;
	noob_keyKind = $arg3; // optional

	noob_scriptKey = (nsl_get_script_key $noob_scriptName);

	if (=s $noob_keyKind "editbind") [
		editbind $noob_scriptKey $noob_scriptAction;
	][
		if (=s $noob_keyKind "specbind") [
			specbind $noob_scriptKey $noob_scriptAction;
		][
			bind $noob_scriptKey $noob_scriptAction;
		]
	]
]

// Checks if the script is enabled, if true then it will create a bind
// @arg1 script name
// @arg2 script function which will be bind to the key
// @arg3 optional - kind of the bind - have to be bind (default), editbind or specbind
// @arg4 optional - should the key get the bind even there isn't a setting if the script gets executed or now?
// @return 
// @example nsl_bind_key_if_script_enabled "statsprinter" "print_stats"
nsl_bind_key_if_script_enabled = [ // deprecated
	local noob_scriptName noob_scriptAction noob_keyKind noob_scriptStatus nslDefaultEnabledState;
	
	noob_scriptName = $arg1;
	noob_scriptAction = $arg2;
	noob_keyKind = $arg3; // optional
	nslDefaultEnabledState = $arg4; // optional
	
	noob_scriptStatus = (nsl_is_script_enabled $noob_scriptName $nslDefaultEnabledState);
	
	if (=s $noob_scriptStatus "true") [
		bindKey $noob_scriptName $noob_scriptAction $noob_keyKind;
	]
	
]

// Only bind a key if the user have set one key in the script options
bindOptionalKey = [
	local noob_scriptName noob_scriptAction noob_keyKind noob_scriptKey;

	noob_scriptName = $arg1;
	noob_scriptAction = $arg2;
	noob_keyKind = $arg3; // optional
	
	noob_scriptKey = (nsl_get_script_key $noob_scriptName);
	if (=s $noob_scriptKey []) [
		result "false";	
	][
		bindKey $noob_scriptName $noob_scriptAction $noob_keyKind;
	]
]

// Check first start after installation or updating the NSL
if (!=s (getalias "nslLastVersionStartup") $nslVersion) [
	
	newgui "Nooby's Script Library" [
		guititle (concatword "Nooby's Script Library v"$nslVersion " (Short: NSL)");
		guitext (concatword "Thank you "(getname)", for installing or updating the NSL!");
		guibar
		guibutton "Let's play!" [showgui main; nslLastVersionStartup = $nslVersion] (playermodelicon);
		
		guitab "Changelog"
		showfileeditor "packages/scripts/nsl_changelog.txt" -60 11
	]

	showgui "Nooby's Script Library";
]

	
// Options
newgui "NSL Options" [
	
	// local toggleNSL toggleOutlaw; don't work here.... :@!
	
	if (=s $nsltoggleNSL "") [
		nsltoggleNSL = 1;
	]
	
	if (=s $nsltoggleOutlaw "") [
		nsltoggleOutlaw = 0;
	]

	nsl_list_scripts = [
		if (= $arg3 1) [
			local fileList file;
			
			fileList = $arg1;
			
			looplist file $fileList [
				guilist [
					local scriptName scriptNameShow status;
				
					scriptName = (nsl_from_file_to_script_name $file);
					scriptNameShow = (strreplace $scriptName "outlaw/" "");
					scriptNameShow = (concat $scriptNameShow " ");
					
					status = (nsl_is_script_enabled $scriptName @@@arg2);
					if (=s $status "true") [ status = 1; ][ status =  0; ]
	
					if (= @@@arg2 "true") [
						guibutton (concat " " $scriptNameShow) [  ] 0
					][
						guitext (concat " " $scriptNameShow)
					]
					
					guispring
					
					guicheckbox "" status 1 0 [ nsl_toggle_script @scriptName; ]
				]
			]
		]
	]
	
	guinoautotab [
		guistayopen [
			local toggleNSLSymbol toggleOutlawSymbol;
			
			if (= $nsltoggleNSL 0) [ toggleNSLSymbol = "+"; ][  toggleNSLSymbol = "-"; ] 
			if (= $nsltoggleOutlaw 0) [ toggleOutlawymbol = "+"; ][  toggleOutlawymbol = "-"; ] 
			
			guibutton (concat $toggleNSLSymbol "NSL") [ if (= $nsltoggleNSL 0) [ nsltoggleNSL = 1; ][ nsltoggleNSL = 0; ]; ] 0 
			nsl_list_scripts $nslAllFiles "true" $nsltoggleNSL;
		
			guibutton (concat $toggleOutlawymbol "Outlaw (needs restart to apply)") [ if (= $nsltoggleOutlaw 0) [ nsltoggleOutlaw = 1; ][ nsltoggleOutlaw = 0; ] ] 0
			nsl_list_scripts $nslAllOutlawFiles "false" $nsltoggleOutlaw;	
		]
	]
]

