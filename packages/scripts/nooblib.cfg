nslVersion = "0.9";
nslSettings = [];

// Executes all scripts in a dir
execdir = [
	local dir file;
	dir = $arg1;
	
	loopfiles f $dir cfg [		
		file = (format "%1/%2.cfg" $dir $f); 
		exec $file;
 	]	
]

// Executes all scripts in all dirs of a list
execdirs = [
	local noob_executeDirs noobexecdirs_dir;
	noob_executeDirs = $arg1;
	
	looplist noobexecdirs_dir $noob_executeDirs [
		execdir $noobexecdirs_dir;
	]
]

// init the nsl
exec_nsl = [
	nslSettings = $arg1;
]

// run outlaw scripts
exec_outlaw = [
	local dir f file;
	dir = "packages/scripts/outlaw";
	
	loopfiles f $dir cfg [	
		if (!=s (nsl_is_script_enabled (concatword "outlaw/" $f ".cfg") "false") "false") [
			file = (format "%1/%2.cfg" $dir $f); 
			exec $file;
		]
 	]	
]

/////////////////////////////////////////////////////
// Get the index of a script in the settings list
// @group runScript
// @arg1 script name
// @return returns a numeric index
// @example getRunScript "autosorry"
getRunScript = [
	local noob_scriptName;
	noob_scriptName = $arg1;
	
	result (indexof $nslSettings $noob_scriptName);
]


/////////////////////////////////////////////////////
// Results if a script should be execute or not
// @group nsl
// @arg1 script name
// @arg2 default setting, used if there isn't a setting saved (optional)
// @return results true or false
// @example nsl_is_script_enabled "autosorry"
nsl_is_script_enabled = [
	local noob_scriptName noob_indexMapData;
	
	noob_scriptName = $arg1;
	
	noob_indexMapData = (getRunScript $noob_scriptName);
	
	if (= $noob_indexMapData -1) [
		if (!=s $arg2 "") [
			result $arg2;
		][
			result "true";
		]
	][
		result (at $nslSettings (+ $noob_indexMapData 1));
	]
]

/////////////////////////////////////////////////////
// Returns the key for a bind for a specific script
// @group runScript
// @arg1 script name
// @return returns a shortcut for a key
// @example getRunScriptKey "autosorry"
getRunScriptKey = [
	local noob_scriptName noob_indexMapData;
	noob_scriptName = $arg1;
	
	noob_indexMapData = (getRunScript $noob_scriptName);
	result (at $nslSettings (+ $noob_indexMapData 2));
]

/////////////////////////////////////////////////////
// Returns the complete options for a script
// @group runScript
// @arg1 script name
// @return returns a cubescript list with all options
// @example getRunScriptOptions "autosorry"
getRunScriptOptions = [
	local noob_scriptName noob_indexMapData;
	noob_scriptName = $arg1;
	
	noob_indexMapData = (getRunScript $noob_scriptName);
	result (at $nslSettings (+ $noob_indexMapData 3));
]

/////////////////////////////////////////////////////
// Results a specific setting out of a settings list
// @group runScript
// @arg1 list with settings e.g. [optionName optionValue optionName2 optionValue2 ...]
// @arg2 optionName
// @arg3 default value, if there isn't a setting saved (optional)
// @return Results the one options value which you are need
// @example getRunScriptSpecificOptions [sayto "team" srymsg "Sorry for that teamkill"] "srymsg"
getRunScriptSpecificOptions = [
	local noob_runScriptOptions noob_runScriptKey indexSpecificOption;

	noob_runScriptOptions = $arg1; // The options list (poor "array" xD)
	noob_runScriptKey = $arg2; // You want this one option
	
	indexSpecificOption = (indexof $noob_runScriptOptions $noob_runScriptKey);
	
	if (= $indexSpecificOption -1) [
		if (!=s $arg3 "") [
			result $arg3;
		][
			result -1;
		]
	][
		result (at $noob_runScriptOptions (+ $indexSpecificOption 1));
	]
]

/////////////////////////////////////////////////////
// Checks (in the settings) if the scripts get executed, if true then it will create a bind
// @group bindKey
// @arg1 script name
// @arg2 script function which will be bind to the key
// @arg3 optional - kind of the bind - have to be bind (default), editbind or specbind
// @return 
// @example ifActiveBindKey "statsprinter" "print_stats"
ifActiveBindKey = [
	local noob_scriptName noob_scriptAction noob_keyKind noob_scriptStatus noob_scriptKey;
	
	noob_scriptName = $arg1;
	noob_scriptAction = $arg2;
	noob_keyKind = $arg3; // optional
	
	noob_scriptStatus = (nsl_is_script_enabled $noob_scriptName);
	
	if (strcmp $noob_scriptStatus "true") [
	
		bindKey $noob_scriptName $noob_scriptAction $noob_keyKind;
		
	]
]

/////////////////////////////////////////////////////
// Same  as "ifActiveBindKey" without the check if the script gets execute 
// @group bindKey
// @arg1 script name
// @arg2 script function which will be bind to the key
// @arg3 optional - kind of the bind - have to be bind (default), editbind or specbind
// @return 
// @example bindKey "statsprinter" "print_stats"
bindKey = [
	local noob_scriptName noob_scriptAction noob_keyKind noob_scriptKey;
	
	noob_scriptName = $arg1;
	noob_scriptAction = $arg2;
	noob_keyKind = $arg3; // optional
	
	noob_scriptKey = (getRunScriptKey $noob_scriptName);
	if (strcmp $noob_keyKind "editbind") [
		editbind $noob_scriptKey $noob_scriptAction;
	][
		if (strcmp $noob_keyKind "specbind") [
			specbind $noob_scriptKey $noob_scriptAction;
		][
			bind $noob_scriptKey $noob_scriptAction;
		]
	]
]

// Only bind a key if the user have set one key in the script options
bindOptionalKey = [
	local noob_scriptName noob_scriptAction noob_keyKind noob_scriptKey;

	noob_scriptName = $arg1;
	noob_scriptAction = $arg2;
	noob_keyKind = $arg3; // optional
	
	noob_scriptKey = (getRunScriptKey $noob_scriptName);
	if (strcmp $noob_scriptKey []) [
	
		result "false";
		
	][
	
		bindKey $noob_scriptName $noob_scriptAction $noob_keyKind;
		
	]
]

// Change a key bind, but only if a specific key have a specific bind at the moment
// @arg1 the key for the bind
// @arg2 the command which should be setted right now
// @arg3 the command which will be bind if the condition is given
createBindIf = [
	local noob_aKey noob_aKeyAction noob_newBindCommand noob_actionOfAKey;
	
	noob_aKey = $arg1;
	noob_aKeyAction = $arg2;
	noob_newBindCommand = $arg3;
	
	noob_actionOfAKey = (getbind $noob_aKey);
	
	if (strcmp $noob_actionOfAKey $noob_aKeyAction) [
		bind $noob_aKey $noob_newBindCommand;
	]	
]

// Count up
counterProgress = [
	local noob_CounterNameProgress;
	
	noob_CounterNameProgress = $arg1;
	
	sleep (getalias (concatword "noob_CounterInterval_" $noob_CounterNameProgress)) [
		if (= (getalias (concatword "noob_CounterStatus_" $noob_uniqueCounterName)) 1) [
		
			(concatword "noob_Counter_" $noob_CounterNameProgress) = (+ (getalias (concatword "noob_Counter_" $noob_CounterNameProgress)) 1);
		
			counterProgress $noob_CounterNameProgress;
		]
	]
]

// Starts a counter
counterStart = [
	local noob_uniqueCounterName noob_counterInterval;
	
	noob_uniqueCounterName = $arg1;
	noob_counterInterval = $arg2;
	
	(concatword "noob_Counter_" $noob_uniqueCounterName) = 0;
	(concatword "noob_CounterStatus_" $noob_uniqueCounterName) = 1;
	(concatword "noob_CounterInterval_" $noob_uniqueCounterName) = $noob_counterInterval;
	
	counterProgress $noob_uniqueCounterName;
]

// Stops a counter
counterStop = [
	local noob_uniqueCounterName;
	
	noob_uniqueCounterName = $arg1;

	(concatword "noob_CounterStatus_" $noob_uniqueCounterName) = 0;
	result (getalias (concatword "noob_Counter_" $noob_uniqueCounterName));
	(concatword "noob_Counter_" $noob_uniqueCounterName) = 0;
]

// Get the value of a counter
counterGet = [
	local noob_uniqueCounterName;
	
	noob_uniqueCounterName = $arg1;
	
	result (getalias (concatword "noob_Counter_" $noob_uniqueCounterName));
]

// Resets a counter
counterReset = [
	local noob_uniqueCounterName noob_newcounterInterval;
	
	noob_uniqueCounterName = $arg1;
	noob_newcounterInterval = $arg2; // optional
	
	(concatword "noob_Counter_" $noob_uniqueCounterName) = 0;
	if (= (strcmp $noob_newcounterInterval "") 0) [
		(concatword "noob_CounterInterval_" $noob_uniqueCounterName) = $noob_newcounterInterval;
	]
]

// Returns 1 (= true) if the mode is a teammode, -1 for false
isTeammode = [
	local noob_checkMode noob_teammodes;
	
	noob_checkMode = $arg1;
	
	noob_teammodes = [2 4 6 8 9 10 11 12 13 14 15 16 17 18 19 20 21 22];
	
	if (!= (indexof $noob_teammodes $noob_checkMode) -1) [
		result 1;
	][
		result -1;
	]
]

// Following Coding is included from the zombie menu:
	// Checks if variable exists, () == null
	nIsDef = [ 
		if (=s (getalias $arg1) "") [ result 0 ] [ result 1 ] 
	] 

	 // Set the var value, if the var dont have already a value
	nInit = [ 
		if (! (nIsDef $arg1)) [alias $arg1 $arg2; result 1 ] [ result 0 ] 
	]
////////////////////////////////////////////////////


"++" = [
	if (= (nIsDef $arg1) 1) [
		alias $arg1 (+ (getalias $arg1) 1);
	][
		alias $arg1 1;
	]
]

"--" = [
	if (= (nIsDef $arg1) 1) [
		alias $arg1 (- (getalias $arg1) 1);
	][
		alias $arg1 -1;
	]
]

// Check first start after installation or updating the NSL
if (! (strcmp (getalias (concatword "nslFirstStartV" $nslVersion)) "1")) [
	
	newgui "Nooby's Script Library" [
		guititle (concatword "Nooby's Script Library v"$nslVersion " (Short: NSL)");
		guitext (concatword "Thank you "(getname)", for installing or updating the NSL!");
		guibar
		guibutton "Let's play!" [showgui main; (concatword "nslFirstStartV" $nslVersion) = 1] (playermodelicon);
		
		guitab "Changelog"
		showfileeditor "packages/scripts/nsl_changelog.txt" -60 11
	]

	showgui "Nooby's Script Library";
]