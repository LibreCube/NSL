if (=s (nsl_is_script_enabled "nooby/noobautosorry" "true") "true") [

	// Load the script options
		noobautosorry_options = (nsl_get_script_options_data "nooby/noobautosorry");

	// Say sorry to the team or to all players?
		noobautosorry_sayToOption = (nsl_get_script_option $noobautosorry_options "sayto");
	
		if (=s $noobautosorry_sayToOption "all") [
			alias noobautosorry_howTo say;
		][
			alias noobautosorry_howTo sayteam;
		]

	// Which message should be posted?
		noobautosorry_srymsg = (nsl_get_script_option $noobautosorry_options "srymsg" "Sorry for that teamkill mate!");
		
	// You did a teamkill? Shame on you!
	noob_autosorry = [
			noobautosorry_lastMode = (getmode); 
			noobautosorry_lastMap = (mapname); 
			noobautosorry_lastFrags = (getfrags); 
			noobsautosorry_lastDeaths = (getdeaths); 
		
		sleep 250 [
			if (&& (=s $noobautosorry_lastMode (getmode)) (=s $noobautosorry_lastMap (mapname))) [
				if (= (isTeammode (getmode)) 1) [
					if (< (getfrags) $noobautosorry_lastFrags) [
						
						if (= (getdeaths) $noobsautosorry_lastDeaths) [
							$noobautosorry_howTo $noobautosorry_srymsg;
						]
					]
				]
			]
			if (= (isconnected) 1) [
				noob_autosorry;
			][
				noob_autosorryCheckIfConnected;
			]
		]	
	]
	
	
	noob_autosorryCheckIfConnected = [
		if (= (isconnected) 1) [
			noobautosorry_lastMode = "";
			noobautosorry_lastMap = ""; 
			noob_autosorry;
		][
			sleep 400 [
				noob_autosorryCheckIfConnected;
			]
		]
	]
	
	noob_autosorryCheckIfConnected;
	
]