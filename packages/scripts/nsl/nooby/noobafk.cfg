// Load the script settings
	noobafk_settings = (nsl_get_script_options_data "nooby/noobafk");

// Init all Settings
	// Settings: Tag
	noobafk_afkTag = (nsl_get_script_option $noobafk_settings "afkTag" ">AFK");
	
	// Settings: TagKind - Prefix or Suffix
	noobafk_afkTagKind = (nsl_get_script_option $noobafk_settings "afkTagKind" "suffix");
	
	// Settings: autoAfk - Would you like to get auto spec + rename if you afk?
	noobafk_autoAfk = (nsl_get_script_option $noobafk_settings "autoAfk" "true");
	
	// Settings: secondsAutoAfk - Seconds until you get autospec + "afk" rename, minimum 30 seconds
	noobafk_secondsAutoAfk = (nsl_get_script_option $noobafk_settings "secondsAutoAfk" 600);
	
	if (< $noobafk_secondsAutoAfk 30) [
		noobafk_secondsAutoAfk = 30;
	]
		
	// Bind defaults:
	createBindIf "UP" "forward" "forward; noobafk_resetAutoAfkCounter;" 
	createBindIf "LEFT" "left" "left; noobafk_resetAutoAfkCounter;";
	createBindIf "RIGHT" "right" "right; noobafk_resetAutoAfkCounter;";
	createBindIf "DOWN" "backward" "backward; noobafk_resetAutoAfkCounter;";

	// Settings: All Keys
	noobafk_keyForward = (searchbinds "forward");
	noobafk_keyLeft = (searchbinds "left");
	noobafk_keyRight = (searchbinds "right");
	noobafk_keyDown = (searchbinds "backward");
	noobafk_keyAttack = (searchbinds "attack");
	noobafk_keyJump = (searchbinds "jump");
	noobafk_keyMsg = (searchbinds "saycommand");
	noobafk_keyBindManually = (nsl_get_script_key "noobafk");
	
	nInit noobafk_keyForwardStore "";
	nInit noobafk_keyLeftStore "";
	nInit noobafk_keyRightStore "";
	nInit noobafk_keyDownStore "";
	nInit noobafk_keyAttackStore "";
	nInit noobafk_keyJumpStore "";
	nInit noobafk_keyMsgStore "";
	
	
	if (=s $noobafk_keyForward "") [
		noobafk_keyForward = $noobafk_keyForwardStore
	][
		createBindIf $noobafk_keyForwardStore  "forward; noobafk_resetAutoAfkCounter;" "forward"
		noobafk_keyForwardStore = $noobafk_keyForward;
	]
	
	if (=s $noobafk_keyLeft "") [
		noobafk_keyLeft = $noobafk_keyLeftStore;
	][
		createBindIf $noobafk_keyLeftStore  "left; noobafk_resetAutoAfkCounter;" "left"
		noobafk_keyLeftStore = $noobafk_keyLeft;
	]
	
	if (=s $noobafk_keyRight "") [
		noobafk_keyRight = $noobafk_keyRightStore;
	][
		createBindIf $noobafk_keyRightStore  "right; noobafk_resetAutoAfkCounter;" "right"
		noobafk_keyRightStore = $noobafk_keyRight;
	]
	
	if (=s $noobafk_keyDown "") [
		noobafk_keyDown = $noobafk_keyDownStore
	][
		createBindIf $noobafk_keyDownStore  "backward; noobafk_resetAutoAfkCounter;" "backward"
		noobafk_keyDownStore = $noobafk_keyDown;
	]
	
	if (=s $noobafk_keyAttack "") [
		noobafk_keyAttack = $noobafk_keyAttackStore;
	][
		createBindIf $noobafk_keyAttackStore  "attack; noobafk_resetAutoAfkCounter;" "attack"
		noobafk_keyAttackStore = $noobafk_keyAttack;
	]
	
	if (=s $noobafk_keyJump "") [
		noobafk_keyJump = $noobafk_keyJumpStore;
	][
		createBindIf $noobafk_keyJumpStore  "jump; noobafk_resetAutoAfkCounter;" "jump"
		noobafk_keyJumpStore = $noobafk_keyJump;
	]
	
	if (=s $noobafk_keyMsg "") [
		noobafk_keyMsg = $noobafk_keyMsgStore;
	][
		createBindIf $noobafk_keyMsgStore  "saycommand; noobafk_resetAutoAfkCounter;" "saycommand"
		noobafk_keyMsgStore = $noobafk_keyMsg;
	]

	noobafk_isAutoAfk = 0;
	noobafk_didIt = 0;
// End if init settings

// Is the script active?
if (=s (nsl_is_script_enabled "nooby/noobafk" "true") "true") [
	noobafk_do = [
		if (&& (!= $noobafk_isAutoAfk 1) (!= $noobafk_didIt 1)) [
			// Don't toggle spectator in coop edit mode.
			if (!= (getmode) 1) [
				spectator 1;
			]
			
			nInit noobafk_restoreName "";
			
			if (=s $noobafk_restoreName "") [
			
				noobafk_restoreName = (getname);
				
				if (=s $noobafk_afkTagKind "prefix") [
					name (concatword $noobafk_afkTag $noobafk_restoreName);
				][
					name (concatword $noobafk_restoreName $noobafk_afkTag);
				]
			]
		]
	]
	
	noobafk_undo = [
		if (|| (= $noobafk_isAutoAfk 1) (= $noobafk_didIt 1)) [
			// Don't toggle spectator in coop edit mode.
			if (!= (getmode) 1) [
				spectator 0;
			]
			
			name $noobafk_restoreName;
			noobafk_restoreName = "";
			
			if (=s $noobafk_autoAfk "true") [
				// Activate the auto afk again
				counterReset "noobafkCounter";
				noobafk_isAutoAfk = 0;
				noobafk_didIt = 0;
				noobafk_autoAfkCheck;
			]
		]
	]
	

	// Auto AFK-Function
	if (=s $noobafk_autoAfk "true") [
		counterStart "noobafkCounter" "1000";
		
		noobafk_autoAfkCheck = [
			sleep 5000 [
				if (= (isconnected) 1) [
					noobafk_Counter = (counterGet "noobafkCounter");
					if (>= $noobafk_Counter $noobafk_secondsAutoAfk) [
						noobafk_do;
						noobafk_isAutoAfk = 1;
					][
						if (>= $noobafk_Counter (- $noobafk_secondsAutoAfk 15)) [
							echo "^f5[ AUTO AFK ] ^f3You will put in spectator in"(- $noobafk_secondsAutoAfk $noobafk_Counter)"seconds, if you don't move, jump or shot NOW!";
						]
					]
				][
					counterReset "noobafkCounter";
				]
				
				if (= $noobafk_isAutoAfk 0) [
					noobafk_autoAfkCheck;
				]
			]
		]
		
		noobafk_autoAfkCheck;
	]	
		
		noobafk_resetAutoAfkCounter = [
			if (= $noobafk_isAutoAfk 0) [
				counterReset "noobafkCounter";
			][
				noobafk_undo;
			]
		]
		
		createBindIf $noobafk_keyForward "forward" "forward; noobafk_resetAutoAfkCounter;" 
		createBindIf $noobafk_keyLeft "left" "left; noobafk_resetAutoAfkCounter;";
		createBindIf $noobafk_keyRight "right" "right; noobafk_resetAutoAfkCounter;";
		createBindIf $noobafk_keyDown "backward" "backward; noobafk_resetAutoAfkCounter;";
		
		createBindIf $noobafk_keyAttack "attack" "attack; noobafk_resetAutoAfkCounter;";
		createBindIf $noobafk_keyJump "jump" "jump; noobafk_resetAutoAfkCounter;";
		createBindIf $noobafk_keyMsg "saycommand" "saycommand; noobafk_resetAutoAfkCounter;";
	
	// Manually AFK-Function
	noobafk_keyPress = [
		if (= (isconnected) 1) [

			if (= $noobafk_didIt 1) [
				noobafk_undo;
				noobafk_didIt = 0;
			][
				noobafk_do;
				noobafk_didIt = 1;
			]
		]
	]

	bindOptionalKey "nooby/noobafk" "noobafk_keyPress";
	
	noobafk_check_if_still_connected = [
		sleep 1000 [
			if (= (isconnected) 0) [
				if (|| (= $noobafk_didIt 1) (= $noobafk_isAutoAfk 1)) [
					noobafk_undo;
					noobafk_didIt = 0;
					noobafk_isAutoAfk = 0;
				]
			]
			
			noobafk_check_if_still_connected;
		]
	]
	
	noobafk_check_if_still_connected;
][
	// The script is deactivated. Check if the keys binds have to be reset.
	createBindIf "UP" "forward; noobafk_resetAutoAfkCounter;" "forward";
	createBindIf "LEFT" "left; noobafk_resetAutoAfkCounter;" "left" 
	createBindIf "RIGHT" "right; noobafk_resetAutoAfkCounter;" "right";
	createBindIf "DOWN" "backward; noobafk_resetAutoAfkCounter;" "backward";

	createBindIf $noobafk_keyForward "forward; noobafk_resetAutoAfkCounter;" "forward";
	createBindIf $noobafk_keyLeft "left; noobafk_resetAutoAfkCounter;" "left" 
	createBindIf $noobafk_keyRight "right; noobafk_resetAutoAfkCounter;" "right";
	createBindIf $noobafk_keyDown "backward; noobafk_resetAutoAfkCounter;" "backward";
	
	createBindIf $noobafk_keyAttack "attack; noobafk_resetAutoAfkCounter;" "attack";
	createBindIf $noobafk_keyJump "jump; noobafk_resetAutoAfkCounter;" "jump";
	createBindIf $noobafk_keyMsg "saycommand; noobafk_resetAutoAfkCounter;" "saycommand";
	
	createBindIf $noobafk_keyBindManually "noobafk_keyPress" "";
]